;
;	File:		SplashScreenINIT.a
;
;	Contains:	code to put up an alert containing a legal notice for seeded software
;
;	Written by:	Chris Derossi
;
;	Copyright:	© 1990 by Apple Computer, Inc., all rights reserved.
;
;	Change History (most recent first):
;
;		 <3>	 4/16/90	KSM		Changed CautionAlert to Alert.
;		 <2>	 3/28/90	csd		replaced the selected text which the check-in script deleted for
;									me.
;		 <1>	 3/28/90	csd		first checked in--puts up a splash screen at boot time for
;									legal, pre-release messages.
;


				LOAD	'StandardEqu.d'

SplashAlert		EQU		-16481					; our alert number

splashScreenFrame	RECORD	0,DECREMENT
saveQDExists 	ds.w	1						; saves QDExists flag
saveA5			ds.l	1
localA5			ds.l	1
thePort			ds.l	1						; my own QuickDraw (required!)
				ds.b	grafSize-4				; other QuickDraw globals (except thePort)
varsSize		equ		*
		ENDR

;---------------------------------------------------------------------
SplashScreen		MAIN	EXPORT
			WITH	splashScreenFrame

            BRA	    @splash
            DC.L	('INIT')					; resource type INIT
            DC.W    12							; id 12
            DC.W    1							; version 1

@splash
			link	a6,#varsSize				; create stack frame

			move.l	a5,saveA5(a6)				; save host A5
			lea		localA5(a6),a5				; setup new A5
			move.b	QDExist,saveQDExists(a6)	; save state of QDExist flag

			pea		thePort(a6)					; ptr to our quickdraw globals
			_InitGraf							; Init our QD world

			_InitFonts							; init the font manager

			_InitWindows						; init the window manager

			_InitMenus							; init the menu manager
			_TEInit								; init TextEdit

			clr.l	-(sp)						; no resume proc
			_InitDialogs

			subq	#2,sp						; room for function result
			move.w	#SplashAlert,-(sp)			; push our alert ID
			clr.l	-(sp)						; no ProcPtr
			_Alert						; put up the alert
			addq.w	#2, sp						; we don’t care about the result
			move.b	saveQDExists(a6),QDExist	; restore state of QDExist flag
			move.l	saveA5(a6),a5				; restore host A5

			unlk	a6							; dump our locals
			rts

			END
